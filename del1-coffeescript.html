<!DOCTYPE html>
<html>
    <head>
        <title>Introduksjon til coffeescript</title>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
        <script src="https://github.com/downloads/gnab/remark/remark-0.3.5.min.js" type="text/javascript"></script>
        <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.7.1/jquery.min.js" type="text/javascript"></script>
        <script src="https://github.com/downloads/kjbekkelund/remark-bekk/bekk-0.0.5.js" type="text/javascript"></script>
        <link href="https://github.com/downloads/kjbekkelund/remark-bekk/bekk-0.0.5.css" type="text/css" rel="stylesheet">
    </head>
    <body>

    <style>
    .small { font-size: 15px; }
    </style>

        <textarea id="source">

.front-page 

# CoffeeScript & Ruby on Rails

## Del 1: Introduksjon til Coffeescript

Kurs for ISFiT, september 2012

v/ Iver Dihle Skjervum & Håkon Erichsen

---

# Hva er CoffeeScript?

CoffeeScript er et dynamisk språk som kompilerer til javascript, men ligner på Ruby eller Python.

Brukes gjerne hvis man...

* ... liker Ruby/Python syntaks
* ... vil unngå mange javascript-feller
* ... vil skrive mindre kode
* ... er litt hipster *

.small[\* Hvis man vil være enda litt mer hipster: Skriv CoffeeScript serverside med node.js!]

---

# Hvordan ser CoffeeScript ut?

En liten smakebit...

Javascript:

    var addTwoNumbers = function (a, b) {
        return a + b;
    }

    addTwoNumbers(1, 2);

CoffeeScript:

    addTwoNumbers = (a, b) ->
        a + b

    addTwoNumbers 1, 2

---

.middle.center

# La oss prøve litt!

[coffeescript.org](http://coffeescript.org/), trykk på 'try coffeescript'.


---

# Basic syntaks

Variabler

    a = 6
    tekst = "hei"
    liste = [1, 2, 3, 4]

If-setninger

    if a is 6
        # Gjør noe
    else if b isnt 2 or a > 7
        # Gjør noe annet
    else
        # Gjør noe heeelt annet

Disse to betyr det samme

    unless b is 2
        # Gjør noe

    if b isnt 2
        # Gjør noe

---

# Basic syntaks

If-kortform

    .coffee
    if ready() then attack() else wait()

Vi har også postfix-notasjon, som i Ruby:

    .coffee
    alert "Great! :)" if organisasjon is "ISFiT"


---

# Basic syntaks

Funksjon uten argument

    siHei = ->
        alert "hei!"

    siHei() # Viktig å ha med parantesene hvis man ikke har noen argumenter

... med argumenter

    pluss = (x, y) ->
        x + y

    pluss 1, 2 # Gir ut 3
    pluss(1, 2) # Gir også ut 3

... med default-parameter:

    siHei = (navn = "ISFiT") ->
        alert "Hei, #{navn}!"

    siHei() # Vil gi ut 'Hei, ISFiT!'    
    siHei('Iver') # Vil gi ut 'Hei, Iver!'  

---

# Basic syntaks

Funksjon som kan ta enn arbitrær mengde argumenter

    test = (a, b...) ->
        # a er en vanlig variabel
        # b blir en liste over alle de overskytende argumentene etter a 

    test()        # Her er a = undefined, b = []  
    test(1, 2, 3) # Her er a = 1, b = [2, 3] 

Du kan også bruke denne notasjonen til å kjøre en funksjon med argumenter fra en liste:

    tall = [1, 2, 3, 4]

    # Disse to gjør det samme:
    Math.max(1, 2, 3, 4)
    Math.max(tall...)

---

# Oppgave

Lag en funksjon `max` som tar inn tre tall, og returnerer det største av dem. Hvis noen er like store er det samme hva den returnerer.
Bruk `alert()` eller `console.log()` til å teste funksjonen.

Hvis du er ferdig: lag en tilsvarende funksjon som kan ta inn en hvilken som helst mengde tall.

NB: Ikke lov å bruke `Math.max`!

---

# Fasit

Mange måter å løse, dette er en simpel en:

    max = (a, b, c) ->

        if a >= b and a >= c
            a
        else if b >= a and b >= c
            b
        else
            c

Mer fancy versjoner:

    max = (a, b, c) ->
        [a, b, c].sort()[2] 

    max = (tall...) ->
        tall.sort()[tall.length - 1] 


Teste funksjonen:

    console.log max(1, 2, 3)
    console.log max(3, 1, 2)
    console.log max(3, 2, 1)

---

# Førsteklasses funksjoner

I JavaScript kan man sende rundt funksjoner.

Altså kan en funksjon knyttes til en variabel, være input til en funksjon, returneres fra en funksjon, osv.

    .coffee
    close = (stuff) ->
      "closing #{stuff}"

    test = close

    alert test("db")

---

# Anonyme funksjoner

Det som egentlig har skjedd så langt er at vi har knyttet en variabel til en *anonym funksjon*.

Her knytter vi f.eks. variabelen `sum` til en funksjon som adderer to input-parametre.

    .coffee
    sum = (a,b) -> a + b

Selve funksjonen er altså kun:

    .coffee
    (a,b) -> a + b

---

# Deklarere objekter

Some å lage en hash i Ruby:

    .coffee
    father = {
      name: 'John',
      daughter: {
        name: 'Jill'
      },
      son: {
        name: 'Jack'
      }
    }

    alert father.son.name

---

# Deklarere objekter

La oss fjerne støyen:

    .coffee
    father =
      name: 'John'
      daughter:
        name: 'Jill'
      son:
        name: 'Jack'

    alert father.son.name

---

# Iterere

Over arrays og ranges:

    .coffee
    nums = [1..10]

    for number in nums
      doStuffWith number

Med sjekk:

    .coffee
    nums = [1..10]

    for number in nums when number > 6
      doStuffWith number

---

# Iterere over objekter

(Legg merke til `of`, istedenfor `in`.)

    .coffee
    me = { name: "Kim Joar", company: "BEKK" }

    for key, value of me
      alert "#{key} = #{value}"

Også her kan vi gjøre en sjekk:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"
      statement: -> "hello world!"

    for key, value of me when typeof value is 'function'
      alert value()

---

# Oppgave

1. Lag en funksjon `map` som tar inn en liste og en funksjon.
2. Input-funksjonen skal kalles for hvert element, og resultatet skal legges i en ny liste

Eksempel på bruk:

    liste = [1, 2, 3, 4]
    funk = (x) -> x * x
    console.log map(liste, funk) # Skal gi [1, 4, 9, 16]

---

# Fasit

    map = (liste, funksjon) ->
        resultat = []

        for item in liste
            resultat.push funksjon(item)

        return resultat


Eller CoffeeScript-måten:

    map = (liste, funksjon) ->
        for item in liste
            funksjon(item)

---

# Sjekk at ting er tilstede eller satt

For å sjekke at et element er tilstede, kan man bruke `in`:

    .coffee
    frukter = ['eple', 'pære', 'banan']
    alert 'eple' in frukter

For å sjekke at en nøkkel er tilstede, kan man bruke `of`:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"

    alert 'name' of me
    alert 'statement' of me

---

# List Comprehensions

    .coffee
    doubles = (n * 2 for n in [1..5])
    alert doubles

Vi kan legge til et filter:

    .coffee
    large = (n for n in [1..5] when n > 2)
    alert large

Det sentrale med list comprehensions er at alle loop-er i CoffeeScript
returnerer en verdi, som er en array bestående av resultatet for hver
gjennomkjøring.


---

# Lexical scoping

.gray[Psst, det er egentlig ganske simpelt og det er helt likt Ruby.]

    .coffee
    outer = 1
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

Og hva skjer her?

    .coffee
    changeNumbers = ->
      outer = 10

    outer = 1
    changeNumbers()
    alert outer

---

# Lexical scoping

Grunnen til dette er at `outer` lever kun i funksjonen i det siste tilfellet. Det
kan vi se om vi gjør det følgende isteden:

    .coffee
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

En variabel lever i et scope, som kan defineres på tre måter:

1. Enhver funksjon lager et scope.
2. En variabel lever i det ytterste scopet der den har blitt satt.
3. Utenfor sitt scope er variabelen usynlig.


---

# this

I Ruby bruker man `self` for å vise til objektet. I CoffeeScript vil dere se
`this` og `@` mange plasser, men ikke bland de med `self` i Ruby.

I CoffeeScript bruker man `this`, *ikke* for å vise til objektet, men for å
vise til *"this context"*.

La oss ta et eksempel.

---

# this

    .coffee
    settNavn = (navn) -> 
        @navn = navn
    settNavn("Håkon")
    alert this.navn

    # Kobler funksjonen til et person-objekt
    person = 
        settNavn: settNavn
    
    person.settNavn("Håkon")
    alert person.navn

Her ser vi først navn satt på det *globale objektet*, og så satt på `person`.

Dette er *veldig* praktisk når vi har førsteklasses funksjoner.

---

# Oppgave

Utvid person-objektet fra forrige slide, og lag en metode `greet`, som knyttes til personen og returnerer "Hei, navn!" der navn er navnet på personen. 

---

# Fasit

    person = {}
    person.settNavn = settNavn
    person.settNavn("Håkon")
    person.greet = -> "Hei, #{@navn}!"

    alert person.greet()

---

# Det var en liten intro!

CoffeeScript har mange flere features, og ikke minst quirks!

... men for å lære må man rett og slett bare skrive og knote litt, så derfor....

---


# JQuery manipulerer DOM-en

    .coffee
    $("#article p.big").html("test")

Input er (nesten) alltid en eller annen selektor (som egentlig bare er CoffeeScriptS + litt ekstra).

Når du har fått et JQuery-objekt kan du gjøre hva-som-helst — for eksempel
traversere, legge til, fjerne, flytte på, iterere, prepende, appende, scrolle,
osvosvosv.

---

.nice.middle

".red[p]" matcher *paragraf*-elementer

".green[#]big" matcher på id-en *big*

".blue[.]test" matcher på klassen *test*

---

# Et eksempel fra koden til denne presentasjonen

    .coffee
    $("code.coffee:contains('alert')")

Dette matcher da HTML-en som ser slik ut:

    .html
    <code class="coffee">
      kodekodekode
      alert
      kodekodekode
    </code>

---

# .on kobler en funksjon til en event

Nettlesere har et sett med innebygde eventer, f.eks.:

* `click`
* `submit`
* `focus`
* `change`
* `mouseOver`

Vi kan dermed kalle en funksjon for eksempel når en bruker klikker på en link.

---

# Eksempel på on med JQuery

    .coffee
    $('.shopping_cart').on 'click', (event) =>
      @customer.purchase @cart

Vi har dermed bundet en funksjon til en klikk på et element. Her ser vi verdien av eventer og verdien av anonyme funksjoner.

Her er et eksempel der verdien av `this` ikke er bundet med `=>`:

    .coffee
    $('#foo').on 'mouseenter mouseleave', (event) ->
      $(this).toggleClass('entered')

Her ser vi verdien av at `this` avhenger av konteksten.

---

# Da må vi prøve litt praktisk!

    <!DOCTYPE html>
    <html>
    <head>
        <script type="text/coffeescript">
        # Her kan du skrive coffeescript
        </script>
        <script src="http://coffeescript.org/extras/coffee-script.js"></script>
        <script src="http://code.jquery.com/jquery-1.8.1.min.js"></script>
    </head>

    <body>
        <div id="person">
            <p class="name">Dag-Inge Aas</p>
            <ul class="barn">
                <li>Reidun</li>
                <li>Rolf</li>
                <li>Ragnar</li>
                <li>Ruth</li>
            </ul>
        </div>

    </body>
    </html>

---

# Oppgave

Alt dette skal gjøres i jQuery

1. Skriv ut alle barna til Dag-Inge i konsollen
2. Gjør teksten på Rolf rød
3. Lag en knapp og et tekstfelt for å legge til nye barn.

---

# Hvordan starter vi med CoffeeScript på en nettside?

Metoden på forrige slide tar litt tid å laste per side, som resulterer i tregere sider. I produksjon bør man derfor aldri bruke den metoden, man bør heller kompilere til JS på forhånd. Da må man:

1. Lag en fil med CoffeeScript i som du kaller f.eks fil.coffee
1. Last ned Node herfra: [http://nodejs.org/#download](http://nodejs.org/#download)
1. Installer CoffeeScript-kompilatoren med `npm install -g coffee-script`
1. Skriv `coffee -c fil.coffee` for å konvertere `fil.coffee` til JS
1. Legg inn `fil.js` som vanlig JS i HTML-filen din.

Huskeregel:

* I produksjon: Bruk alltid `coffee` kommandoen!
* Ellers: Bruk det som er mest praktisk for deg.
* [Stasis](http://stasis.me/) er en veldig grei måte å kompilere CoffeeScript, SASS og mye annet. 
    
    

        </textarea>

        <div id="slideshow" class="white"></div>

    </body>
</html>
